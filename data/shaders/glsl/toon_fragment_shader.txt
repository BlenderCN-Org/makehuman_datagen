varying vec3 vNormal;
varying vec3 vVertex;
uniform sampler2D unit0;

uniform float silhouetteThreshold = 0.2;
uniform float shininess = 20.0;
uniform float ambient = 0.4;
uniform float specular = 0.2;
uniform float diffuse = 0.4;
uniform float specular_th = 0.3;
uniform float diffuse_th = 0.5;

void main (void)
{
  // Texture color
	vec4 tex_color = texture2D(unit0, gl_TexCoord[0].st);

  // Silhouette Color:
  vec4 silhouetteColor = vec4(0.0, 0.0, 0.0, 1.0);

  // Lighting
  //vec3 eyePos = normalize(-vVertex); 
  vec3 lightPos = gl_LightSource[0].position.xyz;

  vec3 Normal = normalize(vNormal);
  vec3 EyeVert = normalize(-vVertex);
  vec3 LightVert = normalize(lightPos - vVertex);
  vec3 EyeLight = normalize(LightVert + EyeVert);

  // Simple Silhouette
  float sil = max(dot(Normal, EyeVert), 0.0);
  if (sil < silhouetteThreshold) 
    gl_FragColor = silhouetteColor;
  else 
  {
    float multiplier = ambient;
    
    // Specular part
    float spec = pow(max(dot(Normal, EyeLight), 0.0), shininess);
    multiplier += specular * smoothstep(specular_th-0.01, specular_th, spec);

    // Diffuse part
    float diff = max(dot(Normal, LightVert), 0.0);
    multiplier += diffuse * smoothstep(diffuse_th-0.01, diffuse_th, diff);
      
    gl_FragColor = multiplier * tex_color;
  }
  
  gl_FragColor.a = tex_color.a;
}
